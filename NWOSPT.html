<HTML>
<HEAD><link rel="shortcut icon" href="/favicon.ico" type="image/x-icon">
<link rel="icon" href="/favicon.ico" type="image/x-icon"></HEAD>
<TITLE>Nwiede Software</TITLE>
<body bgcolor="#3399FF">
<a href="index.html">
   <CENTER> <button>HomePage</button> </CENTER>
</a>
<a href="index.html"> </A><BR>
<!--Website and software created by nwiede. If you happen to come accross any problems in my code be sure to email me!-->
<CENTER>Code Start</CENTER>
<!-- OS code start-->
@OS
CLS
PRINT "----------------"
PRINT "Nwiede Interface"
PRINT "----------------"
INPUT "Type C for calculator, Type G for game1,Credits for credits, and G2 to launch game 2";OS$
INPUT "Type Credits for the credits for the Interface";OS$
INPUT "Type G2 launch game 2";OS$
IF OS$=="C" THEN GOTO @CALC
IF OS$=="Credits" THEN GOTO @CREDITS
IF OS$=="G2" THEN GOTO @GAME2
@CREDITS
COLOR 13
PRINT "Interface and Calculator design by Nwiede"
PRINT "Sample game credit goes to GameBridge, the petit computer devs"
INPUT "Type MENU to go back to the menu";OS$
IF OS$=="MENU" THEN GOTO @END
@CALC
CLS
COLOR 13
INPUT "Enter the first number";NO1
BEEP 3
INPUT "Enter the second number";NO2
BEEP 3
@FCCALC
COLOR 10
INPUT "What Function";OP$
IF OP$=="-" THEN GOTO @SUBT
IF OP$=="/" THEN GOTO @DIVIDE
IF OP$=="+" THEN GOTO @ADD
IF OP$=="x" THEN GOTO @MULTI
IF OP$=="*" THEN GOTO @MULTI
@SUBT
COLOR 11
SUM=NO1-NO2
PRINT "The difference is ";SUM;"
GOTO @END
@DIVIDE
COLOR 11
SUM=NO1/NO2
PRINT "The quotient is ";SUM;"
GOTO @END
@ADD
COLOR 11
SUM=NO1+NO1
PRINT "The sum is ";SUM;"
GOTO @END
@MULTI
COLOR 11
SUM=NO1*NO2
PRINT "The product is ";SUM;"
GOTO @END
<!--Start Of Game 1 code -->
@G1
'+--------------------+
'| Game 1 |
'| Experimental |
'+--------------------+
VISIBLE 1,1,0,0,0,0
CLS:COLOR 0
CLEAR
DIM EX(20),EY(20),ED(20)
'---
EMAX=20
FOR I=0 TO EMAX-1
 EX(I)=FLOOR(I%10)*2
 EY(I)=FLOOR(I/10)*2+3
 ED(I)=0
 LOCATE EX(I),EY(I)
 PRINT "Е"
NEXT
EW=100:EC=0:EDW=FALSE
'---
PX=10:PY=21
LOCATE PX,PY:PRINT"Г";
LIFE=3:SC=0
MX=PX:MY=PY:MST=FALSE
LOCATE 0,22
PRINT"--------------------------------";
LOCATE 0,23
PRINT"Г:";LEFT;
LOCATE 0,0
PRINT"SCORE: ";SC
SV=0
'---
@LOOP
 GOSUB @MYSHIP
 GOSUB @MYSHOT
 GOSUB @ALIEN
 GOSUB @SCORE
 VSYNC 1
GOTO @LOOP
'--- SCORE
@SCORE
IF SV==0 THEN RETURN
SC=SC+10
SV=SV-10
LOCATE 7,0:PRINT SC
RETURN
'--- My SHIP
@MYSHIP
B=BUTTON():VX=0
IF B AND 4 THEN VX=-1
IF B AND 8 THEN VX=1
IF VX==0 THEN @NOTMOVE
LOCATE PX,PY:PRINT" "
PX=PX+VX
IF PX<0 THEN PX=0
IF PX>31 THEN PX=31
 LOCATE PX,PY:PRINT"Г";
'---
@NOTMOVE
RETURN
'--- SHOT
@MYSHOT
IF MST==TRUE THEN @MSMOVE
IF TCHST==FALSE THEN @MSSKIP
MST=TRUE
MX=PX:MY=PY
GOTO @MSMOVE2
'---
@MSMOVE
LOCATE MX,MY:PRINT" "
'---
@MSMOVE2
MY=MY-1
IF MY<3 THEN MST=FALSE:GOTO @MSSKIP
LOCATE MX,MY:PRINT"?"
'---
@MSSKIP
RETURN
'--- ALIEN
@ALIEN
MD=FALSE
IF EDW THEN MD=EDW:EDW=FALSE
'---
FOR I=0 TO EMAX-1
 IF ED(I)==-1 THEN @PASS
 '---
 LOCATE EX(I),EY(I):PRINT" "
 IF MD==FALSE THEN @EMV
 '---
 ED(I)=(ED(I)+1) AND 3
 '---
 @EMV
 GOSUB @EMOVE
 '---
 @SKIP
 IF MST==FALSE THEN @PASS
 IF MX!=EX(I) THEN @PASS
 IF MY!=EY(I) THEN @PASS
 '--- S capital
 LOCATE EX(I),EY(I):PRINT" "
 ED(I)=-1
 MST=FALSE
 SV=SV+10
 '---
@PASS
 '---
NEXT
RETURN
'---
@EMOVE
ON ED(I) GOTO @RI,@DW,@LF,@DW
@DW
EY(I)=EY(I)+1
IF EY(I)>20 THEN EY(I)=3
ED(I)=(ED(I)+1) AND 3
GOTO @EPUT
@RI
EX(I)=EX(I)+1
IF EX(I)<31 THEN @EPUT
EDW=TRUE
GOTO @EPUT
@LF
EX(I)=EX(I)-1
IF EX(I)>0 THEN @EPUT
EDW=TRUE
@EPUT
LOCATE EX(I),EY(I)
PRINT "Е"
RETURN


<!--end of game 1 code -->
<!--Start of game 2 code -->
@GAME2
'+------------------+
'|SAGAME4 |
'| DANMAKU |
'+------------------+
@START
GOSUB @INIT
GOSUB @TITLE
OVR=0
SHIP=3 'Remaining ship
SC=0 'SCORE
STA=4
BOSS=0
NOW=MAINCNTL
FPS=0:CNT=NOW
'--- MAIN LOOP
@MAIN
GOSUB @MYMOVE
GOSUB @MYSHOT
GOSUB @TKMOVE
GOSUB @MAPCTR
FPS=FPS+1
NOW=MAINCNTL
VSYNC 1
IF NOW<CNT THEN CNT=NOW
IF (NOW-CNT)<60 THEN @MAIN
CNT=NOW
LOCATE 0,1
COLOR 0
PRINT " SHIPS ";SHIP;" "
PRINT " SCORE ";SC
IF BOMB==0 THEN COLOR 13
PRINT " BOMBS ";BOMB;" "
IF DEBUG THEN PRINT FPS;"FPS ";
FPS=0
IF MSG THEN MSG=MSG-1
IF MSG THEN @MAIN
A$=" ":GOSUB @PNTSUB3
IF SHIP==0 THEN @START
IF OVR==0 THEN @MAIN
STA=4
OVR=0
GOTO @MAIN
'--- Move ship
@MYMOVE
IF OVR THEN @MYDEAD
BT=BUTTON()
IF (BT AND 1) THEN AY=AY-AD
IF (BT AND 2) THEN AY=AY+AD
IF (BT AND 4) THEN AX=AX-AD
IF (BT AND 8) THEN AX=AX+AD
IF AX<16 THEN AX=16
IF AX>247 THEN AX=247
IF AY<8 THEN AY=8
IF AY>176 THEN AY=176
SPOFS AN,AX,AY
IF STA THEN @MYSTART
'--- Ship collision
IF SPHIT(AN,0)==0 THEN RETURN
J=SPHITNO
IF J<FN OR J>=FN+FMAX THEN RETURN
C=SPGETV(J,0)
IF C==0 THEN RETURN 'Blast
IF C==4 THEN @ONEUP '1UP
IF C==5 THEN @BOMBADD 'BOMB
IF DEBUG THEN RETURN
OVR=1
BEEP 13
GOSUB @MYDEAD
SHIP=SHIP-1
IF SHIP==0 THEN @GMOVER
MSG=1
RETURN
'--- GAME OVER
@GMOVER
BGMN=6
BGMPLAY BGMN
A$="GAME OVER":GOSUB @PNTSUB2
BGMSTOP
RETURN
'---
@PAUSE
COLOR 12
BGMSTOP
I=0
@PAUSE2
IF I==0 THEN A$="→Continue Destruct"
IF I==1 THEN A$=" Continue →Destruct"
GOSUB @PNTSUB3
GOSUB @BTNOFF
GOSUB @BTWAIT
IF (BT AND &HF) THEN I=I XOR 1
IF (BT AND &H4F0)==0 THEN @PAUSE2
CLS
GOSUB @BTNOFF
IF I==0 THEN BGMPLAY BGMN:RETURN
SHIP=0:OVR=1:MSG=3
RETURN
'--- GET 1UP
@ONEUP
BEEP 7
SHIP=SHIP+1
GOTO @ITEMCLR
'--- GET BOMB
@BOMBADD
BEEP 12
BOMB=BOMB+1
@ITEMCLR
SPOFS FN+J,0,999
SPSETV FN+J,4,999 'Y
SPSETV FN+J,1,0 'HP
RETURN
'---Used BOMB
@BOMB
IF BUSY THEN RETURN
IF BOMB==0 THEN RETURN
BUSY=3 'Re-use time
BOMB=BOMB-1
FOR J=0 TO FMAX-1
 GOSUB @TKDAMAGE 'Damage of enemy
NEXT
'--- BOMB visual
FOR I=0 TO BMAX-1
 SPCHR BN+I,143
 SPOFS BN+I,AX,AY
NEXT
FOR I=0 TO BMAX-1
 R=RAD(I*360/BMAX)
 X=AX+COS(R)*300
 Y=AY+SIN(R)*300
 SPOFS BN+I,X,Y,60
NEXT
RETURN
'--- Start my ship
@MYSTART
IF STA<4 THEN @MYSTA2
BEEP 6
AX=128:AY=200
BOMB=3 'Initial equipment BOMB
BUSY=2
@MYSTA2
AY=AY-(STA/4)
STA=STA-0.05
IF STA<=0 THEN STA=0
IF RND(15) THEN RETURN
X=AX:Y=AY+16
NEWR=RAD(90):GOSUB @TKSMOKE
RETURN
'--- My ship explosion
@MYDEAD
BT=0
NEWR=RAD(RND(360))
X=AX:Y=AY:GOSUB @TKSMOKE
SPOFS AN,999,999
IF RND(10)==0 THEN BEEP 37
RETURN
'--- My fire process
@MYSHOT
'---B BUTTON=BOMB
IF (BT AND 32) THEN @BOMB
IF (FPS%8) THEN RETURN
IF BUSY THEN BUSY=BUSY-1:RETURN
'---A BUTTON=SHOT
IF (BT AND 16) THEN @MYFIRE
'---Y BUTTON=SHOT
IF (BT AND 128) THEN @MYFIREY
IF (BT AND 1024) THEN @PAUSE
RETURN
'--- My fire
@MYFIRE
X=AX:Y=AY
X1=0:GOTO @MYFIRE2
@MYFIREY
X=AX:Y=AY
X1= 90
IF (FPS%16)<8 THEN X1=-X1
GOTO @MYFIRE2
@MYFIRE2
I=BN
@MYFIRE3
'--- Search free SPRITE
IF SPCHK(I)==0 THEN @MYFIRE4
I=I+1
IF I<BN+BMAX THEN @MYFIRE3
RETURN 'Not have free SPRITE
@MYFIRE4 'Find free SPRITE
SPCHR I,37
SPOFS I,X,Y
SPOFS I,X+X1,Y-192,30
RETURN
'--- Move enemy
@TKMOVE
FOR I=0 TO FMAX-1
 Y=SPGETV(FN+I,4) 'Coordinate
 IF Y==999 THEN @TKMOVE2
 C=SPGETV(FN+I,0) 'Type
 T=SPGETV(FN+I,2) 'Operating time
 SPSETV FN+I,2,T+1 'Time+1
 X=SPGETV(FN+I,3) 'Coordinate
 R=FR(I) 'Angle
 GOSUB JMPTBL$(C)
 X=X+COS(R)*FD
 Y=Y+SIN(R)*FD
 IF X<-8 OR X>263 THEN Y=999
 IF Y<-31 OR Y>199 THEN Y=999
 SPSETV FN+I,3,X 'Coordinate
 SPSETV FN+I,4,Y '
 SPOFS FN+I,X,Y
 IF FR(I)!=R THEN FR(I)=R:SPANGLE FN+I,FLOOR(DEG(R-TKOF(C)))
 GOSUB @HITCHK
@TKMOVE2
NEXT
RETURN
'--- SMOKE
@TK0
FD=T/15
X=X+RND(5)-2
Y=Y+RND(5)-2
IF T>40 THEN Y=999
RETURN
'--- FIRE
@TK1
FD=2
RETURN
'--- HOMING
@TK2
FD=T/17
IF RND(3) THEN RETURN
GOSUB @TRKING
R=R+(SGN(R1)*0.04)
RETURN
'--- BEAM
@TK3
FD=FLOOR(T*T/200)
IF FD>6 THEN FD=6
RETURN
'--- 1UP
@TK4
FD=0
Y=Y+((T-35)/30)
RETURN
'--- BOMB
@TK5
FD=0.5
X=X+COS(RAD((T*2)%360))*2
RETURN
@TK6
RETURN
@TK7
RETURN
'--- S CURVE
@TK8
FD=2
R1=RAD(180/50)
IF ABS(50-(T%100))<26 THEN R1=-R1
R=R+R1
IF (T%20) THEN RETURN
NEWR=R:GOSUB @TKFIRE
RETURN
'--- SW BACK
@TK9
R=SGN(70-T)*1.57
FD=FLOOR(ABS(60-T)/20)
IF FD>0 THEN RETURN
IF (T%16) THEN RETURN
IF RND(10)>LVL THEN RETURN
BEEP 38
NEWR=RAD(90):NEWC=2:GOSUB @TKBORN
RETURN
'--- V SHOT
@TK10
FD=FLOOR(ABS(60-T)/18)
IF T>100 THEN RETURN
IF FD>0 THEN RETURN
GOSUB @TRKING
R=R+(SGN(R1)*0.05)
IF (T%14) THEN RETURN
NEWR=R-0.3:GOSUB @TKFIRE
NEWR=R+0.3:GOSUB @TKFIRE
BEEP 4
RETURN
'--- U TURN
@TK11
FD=ABS(70-T)/15
R1=RAD(180/140)
IF (I%2) THEN R1=-R1
R=R+R1
IF FD>1 OR (T%15) THEN RETURN
NEWR=ATAN(AY-Y,AX-X)
GOSUB @TKFIRE
BEEP 4
RETURN
'--- SPIN
@TK12
FD=2
IF T>170 THEN RETURN
IF T>100 THEN @TKA12B
IF (T%30) THEN RETURN
J=85+FLOOR(I%2)*85
R=ATAN(64-Y,J-X)
RETURN
@TKA12B
FD=0
IF (T%8) THEN RETURN
R=R+0.8
NEWR=R:GOSUB @TKFIRE
RETURN
'--- UFO
@TK13
FD=1+(T/150)
IF RND(4)==0 THEN R=R-RAD(8)
IF RND(4)==0 THEN R=R+RAD(8)
IF (T%60) THEN RETURN
NEWR=ATAN(AY-Y,AX-X)
GOSUB @TKFIRE
RETURN
'--- HOMING MISSILE
@TK14
IF T>240 THEN J=I:GOTO @TKDEAD
FD=3.5-(T/35)
IF FD<1.6 THEN FD=1.6
GOSUB @TRKING
R=R+(SGN(R1)*0.05)
RETURN
'--- SIDE SHOT
@TK15
FD=FLOOR((T%90)/30)
IF ABS(AY-Y)>20 THEN RETURN
IF (T%20) THEN RETURN
NEWR=RAD( 0):GOSUB @TKFIRE
NEWR=RAD(180):GOSUB @TKFIRE
RETURN
'--- NEAR MISS
@TK16
FD=2.2
J=T%64
IF J THEN RETURN
R=ATAN(120,(AX-48+RND(96))-X)
RETURN
'--- TSUBO
@TK17
J=T%60
FD=0
Y=Y+1
IF J==59 THEN @TK17B
IF J>30 THEN RETURN
GOSUB @TRKING
R=R+(SGN(R1)*0.04)
RETURN
@TK17B
NEWR=R:GOSUB @TKFIRE
RETURN
'---
@TK18
@TK19
RETURN
'--- BOSS1
@TK20
FD=0.5
IF T<90*2 THEN RETURN
FD=0
X=X+COS(RAD((T%90)*4))*2
Y=Y+SIN(RAD((T%40)*9))*2
IF (T%40) THEN RETURN
IF (T%200)<20*LVL THEN @TK20B
FOR NEWR=0 TO 3.14 STEP 0.45
 GOSUB @TKFIRE
NEXT
BEEP 11
RETURN
@TK20B
TY=Y
NEWR=RAD(90)
GOSUB @TKBEAM
Y=Y-32
GOSUB @TKBEAM
Y=Y-32
GOSUB @TKBEAM
Y=TY
BEEP 5
RETURN
'--- BOSS2
@TK21
FD=0.5
IF T<90*2 THEN RETURN
FD=0
J=130-(LVL*4)
IF J<100 THEN J=100
J=T%J
IF J>=90 THEN @TK21B
X=X+COS(RAD(J*4))*1.5
GOSUB @TRKING
R=R+(SGN(R1)*0.01)
IF (T%40) THEN RETURN
FOR NEWR=0 TO 3.14 STEP 0.6
 GOSUB @TKFIRE
NEXT
RETURN
@TK21B
IF (T%5) THEN RETURN
IF RND(14) THEN BEEP 6
TX=X:TY=Y
NEWR=R
X=TX+COS(NEWR)*30
Y=TY+SIN(NEWR)*30
GOSUB @TKBEAM
X=TX:Y=TY
RETURN
'--- BOSS3
@TK22
FD=((90*2)-T)/220
IF T<90*2 THEN RETURN 'Appearance
FD=0
J=T%360
IF J<180 THEN @TK22B
X=X+COS(RAD((J*2)%360))
IF (T%90)==0 THEN @TK22D
IF (T%12)==0 THEN @TK22C
RETURN
@TK22B
Y=Y+(90-J)/40
IF (T%30) THEN RETURN
BEEP 9
FOR NEWR=0 TO 6.28 STEP 0.7
GOSUB @TKFIRE
NEXT
RETURN
@TK22C
IF LVL<RND(20) THEN RETURN
BEEP 38
TX=X 'BACKUP
X=TX+RND(2)*80-40
NEWR=RAD(90)
NEWC=2
GOSUB @TKBORN
X=TX
RETURN
@TK22D
BEEP 11
NEWR=RAD(270)
NEWC=14
GOSUB @TKBORN
RETURN
@TK23
@TK24
@TK25
RETURN
'--- Keep track of my ship
@TRKING
R1=ATAN(AY-Y,AX-X)-R
IF R1> 3.14 THEN R1=R1-6.28
IF R1<-3.14 THEN R1=R1+6.28
RETURN
'--- Enemy+FireCollision
@HITCHK
IF Y==999 THEN RETURN
IF C<8 THEN RETURN 'Invincibble character
IF SPHIT(FN+I,BN)==0 THEN RETURN
J=SPHITNO
IF (J<BN) OR (J>=BN+BMAX) THEN RETURN
IF SPCHK(J)==0 THEN RETURN
SPOFS J,0,999
J=I
@TKDAMAGE
NN=FN+J
IF SPGETV(NN,4)==999 THEN RETURN
C=SPGETV(NN,0)
IF C<1 THEN RETURN
HP=SPGETV(NN,1)
IF HP THEN HP=HP-1 'Enemy life-1
SPSETV NN,1,HP
IF HP THEN BEEP 0,2048:RETURN
X=SPGETV(NN,3) 'Coordinate
Y=SPGETV(NN,4) 'Coordinate
BEEP 13,2096
GOSUB @TKDEAD
SC=SC+1
IF C<8 THEN RETURN
'Item is not used
IF C<20 THEN @HITCHK2
'--- Defeated the boss
@BOSDEAD
BGMN=15
BGMPLAY BGMN
LVL=LVL+1
BOSS=BOSPOS+10000
RETURN
'--- Defeated the enemy
@HITCHK2
IF RND(8)==0 THEN NEWC=4:GOTO @ITEMADD '1UP
IF RND(8)==0 THEN NEWC=5:GOTO @ITEMADD 'BOMB
RETURN
@ITEMADD 'Appearance item
NEWR=RAD(90)
GOSUB @TKBORN2
RETURN
'--- BG process
@MAPCTR
'--- BG0 Scroll through front
HY=(HY-1)AND 511
BGOFS 0,0,HY
GOSUB @DRAWBG0
'--- BG1 Scroll through back
IF (HY%4) THEN RETURN
GY=(GY-1)AND 511
BGOFS 1,0,GY
GOSUB @DRAWBG1
IF OVR OR MSG THEN RETURN
IF BOSS==0 THEN A$="STAGE "+STR$(LVL):BGMN=23:BGMPLAY BGMN:GOSUB @PNTSUB2
BOSS=BOSS+1
IF BOSS>=BOSPOS THEN @BOSS1
'--- Enemy generating process
IF (GY%4) THEN RETURN
IF RND(12)>LVL THEN RETURN
X=RND(256-64)+32:Y=-24
NEWC=FLOOR((GY/(4*5))%10)+8
NEWR=RAD(90)
GOSUB @TKBORN
RETURN
'--- Boss generating process
@BOSS1
J=BOSS-BOSPOS
IF J>10050 THEN BOSS=0:RETURN
IF J>10000 THEN @BOSS3
IF J>1500 THEN @BOSS4
IF J>900 THEN RETURN
IF J>20 THEN @BOSS2
'--- Front of boss
IF BOSS!=BOSPOS THEN RETURN
BGMN=13
BGMPLAY BGMN
A$="WARNING!!":GOSUB @PNTSUB2
RETURN
'--- Boss generating
@BOSS2
X=128:Y=-31
NEWC=20+((LVL-1) % 3)
NEWR=RAD(90)
GOSUB @TKBORN
IF J>=0 THEN BOSS=BOSPOS+900
RETURN
'--- Boss exit
@BOSS3
X=128+RND(128)-64
Y=96+RND(96)-48
NEWR=RAD(RND(360))
IF RND(10)==0 THEN BEEP 13
GOSUB @TKSMOKE
RETURN
'--- Boss destruct
@BOSS4
FOR J=0 TO FMAX-1
 X=SPGETV(FN+J,3) 'Coordinate
 Y=SPGETV(FN+J,4) 'Coordinate
 IF Y!=999 THEN GOSUB @TKDEAD
NEXT
GOSUB @BOSDEAD
RETURN
'--- Enemy explosion
@TKDEAD
NEWC=0
GOTO @TKBORN2
'--- BEAM
@TKBEAM
NEWC=3
GOTO @TKBORN
'--- Explosion
@TKSMOKE
NEWC=0
GOTO @TKBORN
'--- Enemy firing a bullet
@TKFIRE
NEWC=1
IF RND(9)>LVL THEN RETURN
GOTO @TKBORN
'--- Enemy/Effect generating
@TKBORN
J=0
@TKBORN1 'Search free SPRITE
IF SPGETV(FN+J,4)==999 THEN @TKBORN2
J=J+1
IF J<FMAX THEN @TKBORN1
J=-1 'Not have free SPRITE
RETURN
@TKBORN2 'Search free SPRITE
W=TKW(NEWC)
H=TKH(NEWC)
NN=FN+J
SPSET NN,TKC(NEWC),TKPL(NEWC),0,0,1,W,H
SPHOME NN,W/2,H/2
SPCOL NN,-W/2,-H/2,W,H,TRUE
SPOFS NN,X,Y
SPSCALE NN,TKSC(NEWC)
SPANIM NN,TKA(NEWC),8
SPANGLE NN,FLOOR(DEG(NEWR-TKOF(NEWC)))
FR(J)=NEWR
SPSETV NN,0,NEWC 'Type
SPSETV NN,1,TKHP(NEWC) 'Life
SPSETV NN,2,0 'Operating time
SPSETV NN,3,X 'Coordinate
SPSETV NN,4,Y 'Coordinate
RETURN
'---BG0 Front
@DRAWBG0
IF (HY%8) THEN RETURN
PAL=10+((LVL+2)%5)
Y=((HY/8)-1)AND 63
IF BOSS>(BOSPOS+1040) THEN @CLRBG0
FOR X=0 TO 31
 C=0
 IF X<=1 THEN C=796+X+(Y%4)*32
 IF X>=30 THEN C=768+X+(Y%4)*32
 BGPUT 0,X,Y,C,PAL,0,0
NEXT
RETURN
@CLRBG0
FOR X=0 TO 31
 C=0:BGPUT 0,X,Y,C,PAL,0,0
NEXT
RETURN
'---BG1 Back
@DRAWBG1
IF (GY%8) THEN RETURN
Y=((GY/8)-1)AND 63
IF BOSS>BOSPOS THEN @CLRBG1
PAL=10+(LVL%5)
FOR X=0 TO 31
 C=RND(4)+790
 J=ABS(X-16)
 IF J>4 AND ((Y%8)>1) THEN C=0
 BGPUT 1,X,Y,C,PAL,0,0
NEXT
RETURN
@CLRBG1
FOR X=0 TO 31
 C=0:BGPUT 1,X,Y,C,PAL,0,0
NEXT
RETURN
'--- Initialize
@INIT
CLEAR
ACLS
LVL=1
GY=0:HY=0 'BG Coordinate
BOSPOS=300 'Boss generateing timing
BMAX=16 'Maximum number of bullet
FMAX=32 'Maximum number of enemy
TMAX=26 'Type of enemy
DIM FR(FMAX)
DIM TKC(TMAX) 'Character
DIM TKPL(TMAX) 'Palette
DIM TKA(TMAX) 'Number of Anime
DIM TKW(TMAX) 'Width
DIM TKH(TMAX) 'Height
DIM TKSC(TMAX) 'Scale
DIM TKHP(TMAX) 'Life
DIM TKOF(TMAX) 'Offset rotate
DIM JMPTBL$(TMAX)
FOR I=0 TO TMAX-1
 JMPTBL$(I)="@TK"+STR$(I)
NEXT
AN=60 'Ship SPRITE No.
BN=62 'Fire SPRITE No.
FN=0 'Enemy SPRITE No.
'--- Reset Ship
PAL=11
AD=3
W=16:H=32
SPSET AN,288,PAL,0,0,1,W,H
SPHOME AN,W/2,H/2
SPCOL AN,-W/4,-H/4,W/2,H/2,FALSE
SPOFS AN,999,999
'--- Reset Fire
PAL=4
W=16:H=16
FOR I=0 TO BMAX-1
 J=BN+I 'Management number
 SPSET J,37,PAL,0,0,1,W,H
 SPHOME J,W/2,H/2
 SPCOL J,-W/2,-H/2,W,H,FALSE
 SPOFS J,999,999
NEXT
'--- Reset enemy
PAL=3
FOR I=0 TO FMAX-1
 J=FN+I 'Management number
 SPSET J,142,PAL,0,0,1,W,H
 SPHOME J,W/2,H/2
 SPCOL J,-W/2,-H/2,W,H,TRUE
 SPOFS J,999,999
 SPSETV J,1,0 'Life
 SPSETV J,3,999 'Coordinate
 SPSETV J,4,999 'Coordinate
NEXT
'--- Sort character date
DIM BF$(64)
'SPRITE No. 256-287
CHRINIT "SPU4"
BFMAX=2*1*4 'Number of character
FOR C=0 TO 127 STEP BFMAX
 FOR I=0 TO BFMAX-1
  J=C+(I%2)
  J=J+FLOOR((I%4)/2)*4
  J=J+(FLOOR(I/4)%2)*2
  CHRREAD("SPU4",J),BF$(I)
 NEXT
 FOR I=0 TO BFMAX-1
  J=C+I
  CHRSET "SPU4",J,BF$(I)
 NEXT
NEXT
'SPRITE No. 320-383
CHRINIT "SPU5"
BFMAX=2*2*4 'Number of character
FOR C=0 TO 255 STEP BFMAX
 FOR I=0 TO BFMAX-1
  J=C+(I%2)
  J=J+FLOOR((I%4)/2)*4
  J=J+FLOOR(I/8)*8
  J=J+(FLOOR(I/4)%2)*2
  CHRREAD("SPU5",J),BF$(I)
 NEXT
 FOR I=0 TO BFMAX-1
  J=C+I
  CHRSET "SPU5",J,BF$(I)
 NEXT
NEXT
'SPRITE No. 448-511
CHRINIT "SPU7"
BFMAX=4*4*4 'Number of character
FOR C=0 TO 255 STEP BFMAX
 FOR I=0 TO BFMAX-1
  J=C+FLOOR(I/16)*16
  J=J+FLOOR((I%8)/2)*4
  J=J+(FLOOR(I/8)%2)*2
  J=J+(I%2)
  CHRREAD("SPU7",J),BF$(I)
 NEXT
 FOR I=0 TO BFMAX-1
  J=C+I
  CHRSET "SPU7",J,BF$(I)
 NEXT
NEXT
'---
RESTORE @TEKIDATA
FOR I=0 TO TMAX-1
 READ TKC(I)
 READ TKPL(I)
 READ TKA(I)
 READ TKSC(I)
 READ TKW(I),TKH(I)
 READ TKHP(I),TKOF(I)
NEXT
@TEKIDATA
' CHR,PL,AN,SCA,W, H,HP,Offset
DATA 248,12, 8,150,16,16, 1,0
DATA 40, 4, 2,120,16,16, 1,0
DATA 237, 4, 2,120,16,16, 1,0
DATA 169, 3, 1,200,16,16, 1,-0.01
'Item
DATA 288, 4, 1,120,16,16, 4,1.57
DATA 234, 4, 2,120,16,16, 4,1.57
DATA 0, 0, 0, 0 , 0, 0, 0,0
DATA 0, 0, 0, 0 , 0, 0, 0,0
'Enemy
DATA 136, 2, 1,120,16,16, 1,0
DATA 176, 2, 2,180,16,16, 4,0
DATA 376,12, 1,100,32,32, 1,0
DATA 204, 4, 1,120,16,16, 1,0
DATA 174, 3, 2,180,16,16, 2,0
DATA 184, 3, 2,150,16,16, 2,4.71
DATA 272,11, 3,100,32,16, 1,3.14
DATA 178, 3, 6,150,16,16, 6,0
DATA 380,12, 1,100,32,32, 2,3.14
DATA 206,11, 1,150,16,16, 4,-1.57
'Intact
DATA 0, 0, 0, 0 , 0, 0, 0,0
DATA 0, 0, 0, 0 , 0, 0, 0,0
'Boss
DATA 340,12, 1,200,32,32,70,1.56
DATA 372,12, 1,200,32,32,90,3.13
DATA 448,12, 1,120,64,64,90,0
'Still not using
DATA 0, 0, 0, 0 , 0, 0, 0,0
DATA 0, 0, 0, 0 , 0, 0, 0,0
DATA 0, 0, 0, 0 , 0, 0, 0,0
PAL=10
FOR Y=0 TO 63
 GOSUB @CLRBG0 'BG0 Front
 GOSUB @CLRBG1 'BG1 Back
NEXT
'--- GRAPHIC
FOR I=0 TO 400
 X=RND(256):Y=RND(196)
 GPSET X,Y,242+RND(5)
NEXT
RETURN
'---
@TITLE
BGMSTOP
VISIBLE 1,1,0,0,1,1 'BGOFF
CLS
COLOR 0
BEEP 42
RESTORE @TITLE2
Y=4
@TITLE1
READ A$
IF A$=="END" THEN @TITLE3
Y=23
GOSUB @PNTSUB
VSYNC 3
'Y=Y+1
GOTO @TITLE1
@TITLE2
DATA "клклклклклклклклклклклкл"
DATA "л ┣┫ ┗┏┓┯ ┛┯┳┛┗┏┓┛┳━┛ ┛к"
DATA "к ┛ ┛┯┠┛┛┫┛┛━┛┯┠┛┯┛ ┛ ┛л"
DATA "л ┯┗ ┛ ┛┛ ┛┛ ┛┛ ┛┛┃┓┫┠━к"
DATA "к DANMAKU SHOOTING GAMEл"
DATA "лклклклклклклклклклклклк"
DATA ""
DATA "HOW TO PLAY"
DATA ""
DATA "А:Normal Shot"
DATA ""
DATA "Б:Big Bomb "
DATA ""
DATA "П:Move Ship "
DATA ""
DATA ""
DATA ""
DATA ""
DATA ""
DATA "PUSH ANY BUTTON"
DATA ""
DATA "END"
@TITLE3
GOSUB @BTWAIT
GOSUB @BTNOFF
'--- (Secret)Invincible
IF BT AND 4+8 THEN DEBUG=DEBUG XOR 1
'--- (Secret)Stage select
IF BT AND 1 THEN LVL=LVL+1
IF BT AND 2 THEN LVL=LVL-1
IF LVL<1 THEN LVL=1
IF LVL>99 THEN LVL=99
IF (BT AND &HF)==0 THEN @TITLE4
A$="[↑↓]STAGE "+STR$(LVL)+" "
Y=17
GOSUB @PNTSUB
A$="Normal Mode"
IF DEBUG THEN A$="Invincible Mode"
A$="[←→]"+A$
Y=19
GOSUB @PNTSUB
GOTO @TITLE3
'--- Start
@TITLE4
CLS
VISIBLE 1,1,1,1,1,1
RETURN
'--- Text
@PNTSUB2
COLOR 3
MSG=4 'The Display of TIME
@PNTSUB3
Y=96/8
@PNTSUB
X=16-(LEN(A$)/2)
LOCATE X,Y
PRINT A$
RETURN
'--- Button is pushed
@BTWAIT
VSYNC 1
BT=BUTTON()
IF BT==0 THEN @BTWAIT
BEEP 3
RETURN
'--- Button is release
@BTNOFF
VSYNC 1
IF BUTTON() THEN @BTNOFF
RETURN
'+-------------------------+
'|END |
'+-------------------------+

<!--os code end -->
<CENTER>Code End</CENTER>
</body>
</html>
 </HTML>
